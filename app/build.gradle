apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
        mavenCentral() // for spoon-gradle-plugin
    }
    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.14.1'
        classpath 'me.tatarka:gradle-retrolambda:2.5.0'
    }
}

apply plugin: 'spoon'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.protegra.dsparling.testbed"
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    productFlavors {
        mock {
        }

        live {
        }
    }

    // multiple test dependencies share this file...
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.squareup.dagger:dagger:1.2.2'

    compile 'com.android.support:recyclerview-v7:21.0.3'

    compile 'io.reactivex:rxjava:1.0.3'
    compile 'io.reactivex:rxandroid:0.23.0'

    // useful with retrolambda
    compile "org.functionaljava:functionaljava:4.2"

    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger'
    }
    androidTestCompile fileTree(dir: 'testLibs', include: ['*.jar'])
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.1'
}

task checkstyle(type: Checkstyle) {
    configFile file('./config/checkstyle.xml')
    source 'src/main/java'
    include '**/*.java'
    exclude '**/generated/**'

    classpath = files( project.configurations.compile )
}